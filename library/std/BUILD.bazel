load("//cyclone:build_defs.bzl", "cyc_library", "cyclex")

exports_files(
    srcs = [
        "boot_cstubs.c",
        "lineno.cyl",
    ],
    visibility = ["//cyclone/stage1:__subpackages__"],
)

filegroup(
    name = "sources",
    srcs = glob(["*.cyc"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "headers",
    srcs = glob(["*.h"]),
    visibility = ["//visibility:public"],
)

genrule(
    name = "std_doc",
    srcs = glob(["*.h"]) + [
        "//cyclone/library/std",
        "//cyclone/library/std/cyc-lib:cycspecs",
    ],
    outs = ["manual.tex"],
    cmd = " ".join([
        "$(location //cyclone/tools/cycdoc)",
        "-I$(GENDIR)/cyclone/library/std/libc",
        "-cc $(CC)",
        " ".join(["$(location %s)" % hdr for hdr in glob(["*.h"])]),
        "> $@",
    ]),
    toolchains = ["@rules_cc//cc:current_cc_toolchain"],
    tools = ["//cyclone/tools/cycdoc"],
)

cyclex(
    name = "lineno",
    stage = 2,
)

cyc_library(
    name = "std",
    srcs = [
        ":headers",
        ":lineno",
        ":sources",
        "//cyclone/library/std/libc",
    ],
    hdrs = [":headers"],
    copts = ["-D_POSIX_C_SOURCE=200809L"],
    csrcs = [
        "boot_cstubs.c",
        "//cyclone/library/std/libc",
    ],
    linkopts = ["-lpthread"],
    stage = 2,
    visibility = ["//visibility:public"],
)
